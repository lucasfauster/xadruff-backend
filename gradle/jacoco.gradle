apply plugin: 'jacoco'


def coverageLimits = [
        'instruction': 90.00,
        'branch': 90.00,
        'line': 90.00,
        'complexity': 90.00,
        'method': 90.00,
        'class': 90.00
]

jacoco {
    toolVersion = '0.8.7'
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }

    project.ext.exclusions = [
            'src/main/kotlin/com/uff/br/xadruffbackend/controller**',
            'src/main/kotlin/com/uff/br/xadruffbackend/repository**',
            'src/main/kotlin/com/uff/br/xadruffbackend/XadruffBackendApplication*',
            'src/main/kotlin/com/uff/br/xadruffbackend/helper**',
            'src/main/kotlin/com/uff/br/xadruffbackend/exception**',
            'src/main/kotlin/com/uff/br/xadruffbackend/annotation**',
            'src/main/kotlin/com/uff/br/xadruffbackend/model**',
            'src/main/kotlin/com/uff/br/xadruffbackend/config**',
            'src/main/kotlin/com/uff/br/xadruffbackend/dto/request**',
            'src/main/kotlin/com/uff/br/xadruffbackend/dto/response**',
            'src/main/kotlin/com/uff/br/xadruffbackend/dto/enum**',
    ]

    project.ext.exclusionsForJaCoCo = project.ext.exclusions.collect({
        it.replaceAll('src/main/kotlin/', '')
    })

    build.doLast {
        delete fileTree (dir: "${buildDir}/classes", include: "**/JiBX_bindings*.class")
    }


    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: project.ext.exclusionsForJaCoCo)
        })
    }
    doLast {

        def report = file("${jacoco.reportsDir}/test/jacocoTestReport.xml")
        logger.lifecycle("Checking coverage results: ${report}")

        def parser = new XmlParser()
        parser.setFeature("http://apache.org/xml/features/nonvalidating/load-external-dtd", false)
        parser.setFeature("http://apache.org/xml/features/disallow-doctype-decl", false)
        def results = parser.parse(report)

        def percentage = {
            if (it == null) return 0
            def covered = it.'@covered' as Double
            def missed = it.'@missed' as Double
            ((covered / (covered + missed)) * 100).round(2)
        }

        def counters = results.counter
        def metrics = [:]
        metrics << [
            'instruction': percentage(counters.find { it.'@type'.equals('INSTRUCTION') }),
            'branch'     : percentage(counters.find { it.'@type'.equals('BRANCH') }),
            'line'       : percentage(counters.find { it.'@type'.equals('LINE') }),
            'complexity' : percentage(counters.find { it.'@type'.equals('COMPLEXITY') }),
            'method'     : percentage(counters.find { it.'@type'.equals('METHOD') }),
            'class'      : percentage(counters.find { it.'@type'.equals('CLASS') })
        ]

        def failures = []
        logger.quiet("------------------ Current Code Coverage -----------------------")
        metrics.each {
            def limit = coverageLimits[it.key]
            logger.quiet("${it.key} coverage : ${it.value}")
            if (it.value < limit) {
                failures.add("- ${it.key} coverage rate is: ${it.value}%, minimum is ${limit}%")
            }
        }
        logger.quiet("---------------------------------------------------------------")

        if (failures) {
            logger.quiet("------------------ Code Coverage Failed -----------------------")
            failures.each {
                logger.quiet(it)
            }
            logger.quiet("---------------------------------------------------------------")
            throw new GradleException("Code coverage failed")
        } else {
            logger.quiet("Passed Code Coverage Checks")
        }
    }
}

check.dependsOn jacocoTestReport